/*
 * Copyright (c) 2018-2020 NetFoundry, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.artifactory"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    compileOptions {
        targetCompatibility = "8"
        sourceCompatibility = "8"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

repositories {
    jcenter()
    google()
    maven {
        url = "https://netfoundry.jfrog.io/netfoundry/ziti-maven"
    }
}

dependencies {
    api(project(':ziti')){
        exclude module:"lazysodium-java"
    }
    api "com.goterl.lazycode:lazysodium-android:4.1.1@aar"
    api 'net.java.dev.jna:jna:5.5.0@aar'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.72"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}


task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

afterEvaluate {
    publishing {
        publications {
            aar(MavenPublication) {
                groupId = project.group
                version = project.version
                from components.release
                artifact sourceJar
            }
        }
    }
}

artifactory {
    setContextUrl("https://netfoundry.jfrog.io/netfoundry")
    publish {
        repository {
            repoKey = "ziti-maven"
            username = "ziti-ci"
            password = System.getenv("JFROG_API_KEY")
            maven = true
        }
        defaults {
            publications("aar")
            publishPom = true
            publishArtifacts = true
        }
    }
}
