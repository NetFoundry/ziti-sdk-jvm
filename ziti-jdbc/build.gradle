/*
 * Copyright (c) 2018-2021 NetFoundry, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'maven-publish'
    alias(libs.plugins.shadow)
}

ext {
    description "Ziti JDBC (ZDBC) wrapper"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':ziti')
}

tasks.withType(PublishToMavenRepository).all {
    onlyIf { !semver.info.dirty }
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.java.srcDirs
    archiveClassifier = "sources"
}

tasks.register('packageJavadoc', Jar) {
    from javadoc.destinationDir
    archiveClassifier = 'javadoc'
}

shadowJar {
    manifest.from jar.manifest
    archiveClassifier = 'full'
    configurations = [ project.configurations.runtimeClasspath ]
    mergeServiceFiles()
    dependsOn(":ziti:jar")
}

artifacts {
    archives(sourcesJar)
    archives(packageJavadoc)
}

jar {
    manifest {
        attributes 'Main-Class': 'org.openziti.jdbc.CommandLine'
    }
}

publishing {
    publications {
        zdbcJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact packageJavadoc
        }
    }
}

apply from: rootProject.file('publish.gradle')

